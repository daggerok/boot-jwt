buildscript {
  ext {
    appVersion = '0.0.1'
    appGroup = 'daggerok'
    springBootVersion = '1.4.0.RELEASE'
    springCloudVersion = 'Brixton.SR5'
    localGradleVersion = '3.0'
    lombokVersion = '1.16.8'
    propdepsPluginVersion = '0.0.7'
    springSecurityJwtVersion = '1.0.5.RELEASE'
    webjarsLocatorVersion = '0.32'
    webjarsBootstrapVersion = '3.3.7-1'
    webjarsJqueryVersion = '3.1.0'
    webjarsReactVersion = '15.3.1'
    webjarsBabelVersion = '5.8.38'
  }

  dependencies {
    repositories {
      jcenter()
      maven { url 'http://repo.spring.io/plugins-release' }
    }

    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion",
              "org.springframework.build.gradle:propdeps-plugin:$propdepsPluginVersion"
  }
}

// gradle dependencyUpdates -Drevision=release -DoutputFormatter=json,plain -DoutputDir=./build
plugins {
  id 'com.github.ben-manes.versions' version '0.13.0'
}

allprojects {
  version = appVersion
  group = appGroup

  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'propdeps'
  apply plugin: 'propdeps-idea'
  apply plugin: 'propdeps-maven'
  apply plugin: 'propdeps-eclipse'

  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  repositories {
    jcenter()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/libs-milestone' }
  }

  task wrapper(type: Wrapper) { gradleVersion = localGradleVersion }
}

subprojects {
  jar { baseName = "$appGroup-$projectDir.name" }

  apply plugin: 'io.spring.dependency-management'

  dependencyManagement {
    imports { mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion" }
  }

  apply plugin: 'spring-boot'

  springBoot { executable = false }

  dependencies {
    compile "org.projectlombok:lombok:$lombokVersion",
            'org.springframework.boot:spring-boot-starter-actuator'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
  }
}
